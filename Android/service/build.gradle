apply from: 'generator.gradle'
apply plugin: 'com.android.library'

android {
    compileSdkVersion 25
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 25//keep it below M, tests+permission don't work

        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

task buildHelpClass {
    doFirst {
        try {
            def fsep = System.getProperty("file.separator")
            def srcFolder = new File(String.format("%s/src/main/java", getProjectDir().absolutePath).replace("/", fsep))
            if (!srcFolder.exists()) {
                throw new TaskExecutionException(task(name), new Throwable(String.format("'%s' not found!", srcFolder.getAbsolutePath())));
            }
            def classContent = generateHelpClass(srcFolder.absolutePath)
            def classFile = new File(String.format("%s/src/main/java/com/scurab/android/anuitor/reflect/ReflectionHelper.java", getProjectDir().absolutePath).replace("/", fsep))
            classFile.delete()
            classFile.write(classContent, "utf-8")
        } catch (Throwable t) {
            throw new TaskExecutionException(task(name), t)
        }
    }
}

preBuild.dependsOn buildHelpClass

dependencies {
    testCompile 'junit:junit:4.12'
    def supportLib = "25.0.1"
    compile "com.android.support:support-v4:${supportLib}"
    provided "com.android.support:recyclerview-v7:${supportLib}"
    provided "com.android.support:cardview-v7:${supportLib}"
    provided "com.android.support:gridlayout-v7:${supportLib}"
    provided "com.android.support:design:${supportLib}"
    provided "com.android.support:percent:${supportLib}"

    compile 'com.google.code.gson:gson:2.8.0'
    compile 'commons-io:commons-io:2.5'

    compile 'com.google.android.tools:dx:1.7'
    compile 'org.codehaus.groovy:groovy:2.4.8'

    testCompile "org.robolectric:robolectric:3.1.4"
    testCompile 'org.mockito:mockito-core:2.2.22'
    androidTestCompile 'org.mockito:mockito-core:2.2.22'
}
