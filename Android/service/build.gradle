apply from: 'generator.gradle'
apply plugin: 'com.android.library'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 20//keep it below M, tests+permission don't work

        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

task buildHelpClass {
    doFirst {
        try {
            def fsep = System.getProperty("file.separator")
            def srcFolder = new File(String.format("%s/src/main/java", getProjectDir().absolutePath).replace("/", fsep))
            if (!srcFolder.exists()) {
                throw new TaskExecutionException(task(name), new Throwable(String.format("'%s' not found!", srcFolder.getAbsolutePath())));
            }
            def classContent = generateHelpClass(srcFolder.absolutePath)
            def classFile = new File(String.format("%s/src/main/java/com/scurab/android/anuitor/reflect/ReflectionHelper.java", getProjectDir().absolutePath).replace("/", fsep))
            classFile.delete()
            classFile.write(classContent, "utf-8")
        } catch (Throwable t) {
            throw new TaskExecutionException(task(name), t)
        }
    }
}

preBuild.dependsOn buildHelpClass

dependencies {
    testCompile 'junit:junit:4.12'
    def supportLib = "23.1.1"
    compile "com.android.support:support-v4:${supportLib}"
    provided "com.android.support:recyclerview-v7:${supportLib}"
    provided "com.android.support:cardview-v7:${supportLib}"
    provided "com.android.support:gridlayout-v7:${supportLib}"
    provided "com.android.support:design:${supportLib}"
    provided "com.android.support:percent:${supportLib}"

    compile 'com.google.code.gson:gson:2.4'
    compile 'commons-io:commons-io:2.4'

    testCompile "org.robolectric:robolectric:3.0"
    testCompile 'org.mockito:mockito-core:2.0.41-beta'
    androidTestCompile 'org.mockito:mockito-core:2.0.41-beta'
}
