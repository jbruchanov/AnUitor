apply from: 'generator.gradle'
apply plugin: 'com.android.library'

android {
    compileSdkVersion compileSdk
    buildToolsVersion buildTools

    defaultConfig {
        minSdkVersion minSdk
        targetSdkVersion targetSdk//keep it below M, tests+permission don't work

        versionCode 1
        versionName "1.0"
        if (groovyEnabled) {
            multiDexEnabled true
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    android {
        lintOptions {
            abortOnError false
        }
    }
}

task buildHelpClass {
    doFirst {
        try {
            def fsep = System.getProperty("file.separator")
            def srcFolder = new File(String.format("%s/src/main/java", getProjectDir().absolutePath).replace("/", fsep))
            if (!srcFolder.exists()) {
                throw new TaskExecutionException(task(name), new Throwable(String.format("'%s' not found!", srcFolder.getAbsolutePath())));
            }
            def classContent = generateHelpClass(srcFolder.absolutePath)
            def classFile = new File(String.format("%s/src/main/java/com/scurab/android/anuitor/reflect/ReflectionHelper.java", getProjectDir().absolutePath).replace("/", fsep))
            classFile.delete()
            classFile.write(classContent, "utf-8")
        } catch (Throwable t) {
            throw new TaskExecutionException(task(name), t)
        }
    }
}

preBuild.dependsOn buildHelpClass

dependencies {
    testCompile 'junit:junit:4.12'

    compileOnly "com.android.support:support-core-ui:${supportLib}"
    compileOnly "com.android.support:support-fragment:${supportLib}"
    compileOnly "com.android.support:recyclerview-v7:${supportLib}"
    compileOnly "com.android.support:cardview-v7:${supportLib}"
    compileOnly "com.android.support:gridlayout-v7:${supportLib}"
    compileOnly "com.android.support:percent:${supportLib}"
    compileOnly "com.android.support:design:${supportLib}"

    compile "com.google.code.gson:gson:${gson}"
    compileOnly "com.google.android.tools:dx:${dx}"
    compileOnly "org.codehaus.groovy:groovy:${groovy}"

    //androidTest only, not needed for the lib itself
    testCompile "commons-io:commons-io:${commonsIo}"
    testCompile "org.robolectric:robolectric:${robolectric}"
    testCompile "org.mockito:mockito-core:${mockito}"
    androidTestCompile "org.mockito:mockito-core:${mockito}"
}
